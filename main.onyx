package main

use core {*}
use core.io { Reader }
use core {alloc}
use weave {*}

#load "src/module.onyx"

program :: () => {
    return Console.print(0, "Type something: ", (ctx) => {
        return Console.read(0, (ctx, txt) => {
            return Console.print(.{txt=txt}, "closure test (should print what you typed): ", (ctx) => {
                return Console.print("hoopla", ctx.txt, (ctx) => {
                    return ctx
                })
            })
        })
    })
}

MyHandler :: struct {
    handler :: #match {
        macro (e: Console.Print($K, $C)) => {
            printf("{}\n", e.p)
            return e.k(e.ctx)
        }
        macro (e: Console.Read($K, $C)) => {
            stdin_reader := Reader.make(&stdio.stream)
            defer Reader.free(&stdin_reader)
            line := stdin_reader |> .read_line |> .strip_whitespace
            return e.k(e.ctx, line)
        }
    }
}

console_map :: (f: () -> $T) -> T {
    return f()
}

main :: () {
    println(typeof program)

    result := program
            |> console_map
            |> run(MyHandler)

    println(typeof result)
    println(result)
}








