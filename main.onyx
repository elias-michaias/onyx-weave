package main

use core {*}
use core.io { Reader }
use core {alloc}
use weave {*}

#load "src/module.onyx"

program :: () => {
    return Console.print("Type something: ", 0, (ctx) => {
        return Console.read(0, (txt, ctx) => {
            return Console.print("closure test (should print what you typed): ", .{txt=txt}, (ctx) => {
                return Console.print(ctx.txt, "hoopla", (ctx) => {
                    return ctx
                })
            })
        })
    })
}

MyHandler :: struct {
    handler :: #match {
        macro (e: Console.Print($K, $C)) => {
            printf("{}\n", e.p)
            return e.k(e.ctx)
        }
        macro (e: Console.Read($K, $C)) => {
            stdin_reader := Reader.make(&stdio.stream)
            defer Reader.free(&stdin_reader)
            line := stdin_reader |> .read_line |> .strip_whitespace
            return e.k(line, e.ctx)
        }
    }
}

main :: () {
    println(typeof program)

    result := program()
            |> run(MyHandler)

    println(typeof result)
    println(result)
}








